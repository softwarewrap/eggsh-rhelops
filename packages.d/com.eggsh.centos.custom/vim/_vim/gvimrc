" DO NOT MAKE CHANGES TO THIS FILE =================================
" Instead, place custom changes in $HOME/.vim/plugin/rc.vim

" GLOBAL SETTINGS ==================================================
set nocompatible
syntax on
filetype on

let mapleader=","
set path+=$VIMRUNTIME
set laststatus=2 cmdheight=2 mousehide wrap number nofoldenable
set ai nocindent ignorecase expandtab incsearch hlsearch
set tabstop=3 shiftwidth=3 previewheight=24 formatoptions=l backspace=indent,eol,start t_kb=
set splitright nosplitbelow noequalalways linebreak showbreak=\ \ \ 
set sessionoptions+=winpos,resize
set statusline=%<%f%h%w%m%r%=%l\ of\ %L\ col\ %c%V,\ char\ %o\ %P\ x%B
set cpo-=<
set background=light
set guioptions+=b
set guioptions-=T
set fileformats=dos,mac,unix
set printoptions=header:0
set mps+=<:>
set viminfo='50,n$HOME/.vim/.viminfo
set ffs=unix,dos,mac
set backup writebackup backupdir=$HOME/.vim/save directory=$HOME/.vim/swp// patchmode=.save
set cursorline
set t_Co=256
set notimeout nottimeout
set t_BE=
let c_minlines=400
let savevers_dirs=&backupdir
let g:showmarks_hlline_upper=1
let g:showmarks_include="abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ"
let g:MultipleSearchColorSequence="#ffff33,#99ff99,#aaffff,#ccccff,#ffcccc"
let g:MultipleSearchTextColorSequence="#000000,#000000,#000000,#000000,#000000"
let g:MultipleSearchMaxColors=5
let g:bufExplorerSortBy='name'
let g:bufExplorerShowRelativePath=1

colorscheme summerfruit256
hi MyGroup ctermfg=0 ctermbg=219 guifg=#000000 guibg=#ffafff
hi ColorColumn ctermbg=255 guibg=#eeeeee

fu! RefreshEggHighlighting()
   let P_Name=':[a-zA-Z0-9\.-]\+'
   let P_RE='\(' . '@\?' . '\|' . P_Name . '\)'

   let C_Name=':[%a-zA-Z][%a-zA-Z0-9_\.-]*'
   let C_RE='\(' . '\(' . C_Name . '\)\?' . '\|' . '\(' . '+' . '\|' . P_Name . '\)' . C_Name . '\)'

   let U_Name=':[%a-zA-Z][%a-zA-Z0-9_\.-]*'
   let U_RE='\(' . U_Name . '\)\?'

   let FunctionRE='\(:[a-zA-Z_:][a-zA-Z0-9_,:]*\)\?'
   let FunctionAsVarRE='\(_[a-zA-Z_][a-zA-Z0-9_,]*\)'

   let P_Function='(@' . P_RE . ')' . FunctionRE
   let P_Directory='(@' . P_RE . '/)'
   let P_Directory='\(' . P_Directory . '\|' . '(/)' . '\|' . '(=)' . '\)'
   let P_Variable='(@' . P_RE . ')' . FunctionAsVarRE

   let C_Function='(+' . C_RE . ')' . FunctionRE
   let C_Directory='(+/)'
   let C_Variable='(+' . C_RE . ')' . FunctionAsVarRE

   let U_Function='(-' . U_RE . ')' . FunctionRE
   let U_Variable='(-' . U_RE . ')' . FunctionAsVarRE

   let F_Variable='(\.)' . FunctionAsVarRE

   call matchadd('eggshPackageFunction', P_Function, -1)
   call matchadd('eggshPackageDirectory', P_Directory, -1)
   call matchadd('eggshPackageVariable', P_Variable, -1)

   call matchadd('eggshComponentFunction', C_Function, -1)
   call matchadd('eggshComponentDirectory', C_Directory, -1)
   call matchadd('eggshComponentVariable', C_Variable, -1)

   call matchadd('eggshUnitFunction', U_Function, -1)
   call matchadd('eggshUnitVariable', U_Variable, -1)

   call matchadd('eggshFunctionVariable', F_Variable, -1)

   " Escaped (Not a scope)
   call matchadd('eggshEscaped','\\' . P_Function, -1)
   call matchadd('eggshEscaped','\\' . P_Directory, -1)
   call matchadd('eggshEscaped','\\' . P_Variable, -1)

   call matchadd('eggshEscaped','\\' . C_Function, -1)
   call matchadd('eggshEscaped','\\' . '(+/)', -1)
   call matchadd('eggshEscaped','\\' . C_Variable, -1)

   call matchadd('eggshEscaped','\\' . U_Function, -1)
   call matchadd('eggshEscaped','\\' . U_Variable, -1)

   call matchadd('eggshEscaped','\\' . F_Variable, -1)
   echo
endfunc

" Fonts
fu! FontBigger()
    try
        throw $currentFontSize
    catch /^6/
        se guifont=Lucida\ Console\ Semi-Condensed\ 8
        let $currentFontSize=8
    catch /^8/
        se guifont=Lucida\ Console\ Semi-Condensed\ 9
        let $currentFontSize=9
    catch /^9/
        se guifont=Lucida\ Console\ Semi-Condensed\ 10
        let $currentFontSize=10
    catch /^10/
        se guifont=Lucida\ Console\ Semi-Condensed\ 11
        let $currentFontSize=11
    catch /^11/
        se guifont=Lucida\ Console\ Semi-Condensed\ 12
        let $currentFontSize=12
    catch /^12/
        se guifont=Lucida\ Console\ Semi-Condensed\ 14
        let $currentFontSize=14
    catch /^14/
        se guifont=Lucida\ Console\ Semi-Condensed\ 16
        let $currentFontSize=16
    catch /^16/
        se guifont=Lucida\ Console\ Semi-Condensed\ 18
        let $currentFontSize=18
    catch /^18/
        se guifont=Lucida\ Console\ Semi-Condensed\ 24
        let $currentFontSize=24
    catch /^24/
        se guifont=Lucida\ Console\ Semi-Condensed\ 36
        let $currentFontSize=36
    catch /^36/
        se guifont=Lucida\ Console\ Semi-Condensed\ 48
        let $currentFontSize=48
    catch /^48/
        se guifont=Lucida\ Console\ Semi-Condensed\ 72
        let $currentFontSize=72
    catch /.*/
    endtry
endfunc

fu! FontSmaller()
    try
        throw $currentFontSize
    catch /^72/
        se guifont=Lucida\ Console\ Semi-Condensed\ 48
        let $currentFontSize=48
    catch /^48/
        se guifont=Lucida\ Console\ Semi-Condensed\ 36
        let $currentFontSize=36
    catch /^36/
        se guifont=Lucida\ Console\ Semi-Condensed\ 24
        let $currentFontSize=24
    catch /^24/
        se guifont=Lucida\ Console\ Semi-Condensed\ 18
        let $currentFontSize=18
    catch /^18/
        se guifont=Lucida\ Console\ Semi-Condensed\ 16
        let $currentFontSize=16
    catch /^16/
        se guifont=Lucida\ Console\ Semi-Condensed\ 14
        let $currentFontSize=14
    catch /^14/
        se guifont=Lucida\ Console\ Semi-Condensed\ 12
        let $currentFontSize=12
    catch /^12/
        se guifont=Lucida\ Console\ Semi-Condensed\ 11
        let $currentFontSize=11
    catch /^11/
        se guifont=Lucida\ Console\ Semi-Condensed\ 10
        let $currentFontSize=10
    catch /^10/
        se guifont=Lucida\ Console\ Semi-Condensed\ 9
        let $currentFontSize=9
    catch /^9/
        se guifont=Lucida\ Console\ Semi-Condensed\ 8
        let $currentFontSize=8
    catch /^8/
        se guifont=Lucida\ Console\ Semi-Condensed\ 6
        let $currentFontSize=6
    catch /.*/
    endtry
endfunc

let $currentFontSize=11
call FontBigger()

" Toggle word wrapping
let $wrapState=1
fu! ToggleWrap()
    if $wrapState == 0
        set wrap
        let $wrapState=1
    else
        set nowrap
        let $wrapState=0
    endif
endfunc
map ,w :call ToggleWrap()<CR>

" Toggle what happens when you press the ENTER key (selection)
fu! ToggleKeywordIsPropertyState()
    if $nextKeywordIsPropertyState == 0
        set iskeyword=@,48-57,_,192-255,@-@,+,-,.,(,),:
        let $nextKeywordIsPropertyState=1
        echo "ENTER key now also selects extended function and variable set"
    elseif $nextKeywordIsPropertyState == 1
        set iskeyword=@,48-57,_,192-255,.
        let $nextKeywordIsPropertyState=2
        echo "ENTER key now also selects dotted properties"
    else
        set iskeyword=@,48-57,_,192-255
        let $nextKeywordIsPropertyState=0
        echo "ENTER key selects: Standard characters only"
    endif
endfunc
let $nextKeywordIsPropertyState=0
set iskeyword=@,48-57,_,192-255

let g:highlightword = 1
fu! ToggleHighlightWord()
   if g:highlightword == 0
      let g:highlightword = 1
      echo "Full word only highlighting"
   else
      let g:highlightword = 0
      echo "Partial word highlighting"
   endif
endfunc

let g:highlighting = 0
function! Highlighting()
  if g:highlightword == 1
     if g:highlighting == 1 && @/ =~ '^\\<'.expand('<cword>').'\\>$'
       let g:highlighting = 0
       return ":silent nohlsearch\<CR>"
     endif
  else
     if g:highlighting == 1 && @/ =~ '^'.expand('<cword>').'$'
       let g:highlighting = 0
       return ":silent nohlsearch\<CR>"
     endif
  endif

  if g:highlightword == 1
     let @/ = '\<'.expand('<cword>').'\>'
   else
     let @/ = expand('<cword>')
   endif
  let g:highlighting = 1
  return ":silent set hlsearch\<CR>yiw"
endfunction
nnoremap <silent> <expr> <CR> Highlighting()

" Toggle line numbering
fu! ToggleNumbering()
    if $numberingState == 0
        let $numberingState=1
        set nu foldcolumn=3
        ShowMarksToggle
    else
        let $numberingState=0
        set nonu foldcolumn=0
        ShowMarksToggle
    endif
endfunc
let $numberingState=1

" Toggle fold visibility
fu! ToggleFold()
   if (&foldenable)
      set nofoldenable
      set foldcolumn=0
   else
      set foldenable
      set foldmethod=indent
      set foldnestmax=9
      let javaScript_fold=1         " JavaScript
      let perl_fold=1               " Perl
      let php_folding=1             " PHP
      let r_syntax_folding=1        " R
      let ruby_fold=1               " Ruby
      let sh_fold_enabled=1         " sh
      let vimsyn_folding='af'       " Vim script
      let xml_syntax_folding=1      " XML
      set foldcolumn=3
   endif
endfunc

" Toggle displaying of errors
fu! ToggleDisplayErrors()
    if g:display_error_state
        hi link postscrError Normal
        hi link postscrHexString Normal
        hi link htmlError Normal
        hi link shDerefWordError Normal
        hi! link Error Normal
    else
        hi link postscrError Error
        hi link postscrHexString postscrString
        hi link htmlError Error
        hi link shDerefWordError Error
        hi! link Error NONE
    endif
    let g:display_error_state = ! g:display_error_state
endfunc
let g:display_error_state = 1
call ToggleDisplayErrors()

fu! ToggleColorColumns()
   if w:display_color_columns
      let &colorcolumn=w:display_color_column_set
   else
      set colorcolumn=
   endif
    let w:display_color_columns = ! w:display_color_columns
endfunc
fu! ToggleColorColumnSet()
   if w:display_color_column_set == "49,100"
      let w:display_color_column_set = "43,49,100"
   elseif w:display_color_column_set == "43,49,100"
      let w:display_color_column_set = ""
   elseif w:display_color_column_set == ""
      let w:display_color_column_set = "49,100"
   endif
   let &colorcolumn=w:display_color_column_set
endfunc

fu! SetBufWinEnter()
   let w:display_color_column_set = "49,100"
   let w:display_color_columns = 1
endfunc

auto BufWinEnter * call SetBufWinEnter()

map ,* yiw:match MyGroup /<c-r>"/<CR>

" Decrease fold level
map ,< zm
" Increase fold level
map ,> zr

" Function key mappings
" Highlighting
map <f1> :noh<cr>:match<cr>:call RefreshEggHighlighting()<cr>
map <S-f1> :call ToggleDisplayErrors()<cr>
" Folds: f2=enabled/disabled, f3=open/close
map <f2> :call ToggleFold()<CR>
map <f3> zA
" Show version differences for a file (left: current, right: history)
" Use numeric + and numeric - to scroll through history
map <f4> :call ToggleVersDiff()<cr>
map <silent> <S-f4> :call EndVersDiff()<cr>
map <f6> :call ToggleKeywordIsPropertyState()<CR>
map <S-f6> :call ToggleHighlightWord()<CR>
map <f7> :call FontSmaller()<CR>:echo "Font size: " . $currentFontSize<CR>
map <f8> :call FontBigger()<CR>:echo "Font size: " . $currentFontSize<CR>
map <f9> :call ZoomWin()<CR>
map <f10> :call ToggleNumbering()<CR>
" Toggle color columns (to help user make alignment decisions)
map <f11> :call ToggleColorColumns()<CR>
" Change the set of color columns that are being used
map <S-f11> :call ToggleColorColumnSet()<CR>
" Save/Restore the session
map <f12> :mksession! $HOME/.vim/vimsession.vim<CR>
map <s-f12> :so $HOME/.vim/vimsession.vim<CR>

" ===== Miscellaneous =====
" Search for Eggsh token
map ,k /%{[^}]*}%<cr>

" Read a template into the current buffer
map ,t :r $VIM/template/

" Open Buffer Explorer: easily navigate between buffers
map ,x :BufExplorer<CR>

" Yank to end of line
map Y y$

" Replay macro a (Record a macro: qa ... q, then replay with <C-A>)
map <C-A> @a

" Move to the next file
map <C-N> :n<cr>

" ===== Copy, Delete, and Paste =====
" Mark the end of a block
map ,e me
" Copy to the mark
map ,c "ay'e
" Copy and Remove content to the mark
map ,r "ay'ed'e
" Put copied content below cursor line
map ,p "ap
map ,v "ap
" Put coopied content above cursor line
map ,P "aP
map ,V "aP

" Resizing Windows
map <M-Left> <C-W><
map <M-Up> <C-W>+
map <M-Down> <C-W>-
map <M-Right> <C-W>>

" Split the current window, opening a new window in the direction indicated
map <C-S-Right> :vnew<CR>
map <C-S-Left> :se nosplitright<CR>:vnew<CR>:se splitright<CR>
map <C-S-Down> :se splitbelow<CR>:new<CR>
map <C-S-Up> :se nosplitbelow<CR>:new<CR>

" Move to a window in the direction indicated
map <C-Right> <c-w>l
map <C-Left> <c-w>h
map <C-Up> <c-w>k
map <C-Down> <c-w>j

" Rotate windows to the right
map <C-M-Right> <c-w>r

" Update current buffer with any changes that are on disk, but not in the buffer yet
map <C-U> M:e!<cr>

" Terminate vim without writing
map <C-T> :qa!<cr>

" Terminate vim, but save all files first
map <C-M-T> :xa!<cr>

" Show the vim syntax type under the cursor
function! SynStack()
    if !exists('*synstack')
        return
    endif
    echo map(synstack(line('.'), col('.')), "synIDattr(v:val, 'name')")
endfunc
map <s-f10> :call SynStack()<CR>

" Vim diffing
let $diffWhiteState=1
fu! ToggleDiffWhite()
    if $diffWhiteState == 0
        set diffopt-=iwhite
        let $diffWhiteState=1
    else
        set diffopt+=iwhite
        let $diffWhiteState=0
    endif
endfunc

fu! ToggleVersDiff()
   if $versdiffState == 0
      let $versdiffState=1
      nmap <silent> - :VersDiff -<cr>
      nmap <silent> + :VersDiff +<cr>
      nmap <silent> <kMinus> :VersDiff -<cr>
      nmap <silent> <kPlus> :VersDiff +<cr>
      VersDiff -
   else
      let $versdiffState=0
      unmap <silent> -
      unmap <silent> +
      unmap <silent> <kMinus>
      unmap <silent> <kPlus>
      VersDiff -c
   endif
endfunc
fu! EndVersDiff()
   Purge 0
   w!
endfunc
let $versdiffState=0

fu! DiffSetup()
    auto GUIEnter * simalt ~x
    auto BufEnter * let &titlestring = expand("%:t")

    set diffopt=filler,context:9999
    set cursorbind scrollbind cursorline
    map <f5> :diffupdate<CR>
    map <right> :diffget<CR>
    map <left> :diffput<CR>
    map <down> ]cz.<c-w><c-w><c-w><c-w>
    map <up> [cz.<c-w><c-w><c-w><c-w>
    map <s-down> do
    map <s-up> dp
    map <m-down> <c-w>wyy<c-w>wPjdd
    map <m-s-down> <c-w>wyy<c-w>wPj
    map u :u<cr>:dif<cr>
    map U :red<cr>:dif<cr>
    let $currentFontSize=8
    call FontBigger()
    hi clear CursorLine
    hi CursorLine cterm=underline
endfunc
fu! DiffThis()
    call DiffSetup()
    exe "normal \<C-W>h1G"
    diffthis
    exe "normal \<C-W>l1G"
    diffthis
endfunc
map ,d :call DiffThis()<CR>
map ,i :call ToggleDiffWhite()<CR>

auto BufNewFile,BufRead *.conf set filetype=conf
auto BufNewFile,BufRead * if &syntax == '' | set syntax=sh | endif

if &diff
    call DiffSetup()
else
   auto BufEnter * let &titlestring = expand("%:t")
   auto BufNewFile,BufRead * call RefreshEggHighlighting()
endif

" DO NOT MAKE CHANGES TO THIS FILE =================================
" Place local changes in $HOME/.vim/plugin/rc.vim
